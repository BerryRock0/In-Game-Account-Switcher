plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "InGameAccountSwitcher-Fabric-1.14"
version = project.mod_version
evaluationDependsOn(':shared')

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://api.modrinth.com/maven/" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.14.4"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.28.5+1.14"
    modImplementation "io.github.prospector:modmenu:1.7.17+build.1"
    modRuntimeOnly "maven.modrinth:lazydfu:0.1.2"
    compileOnly project(":shared")
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 8
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileJava {
    source project(':shared').sourceSets.main.allSource
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}