plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
}

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
group = "ru.vidtu.ias"
base.archivesName = "InGameAccountSwitcher-Fabric-1.20.4"
version = project.mod_version
evaluationDependsOn(':shared')

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://api.modrinth.com/maven/" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.20.4"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.91.3+1.20.4"
    modImplementation "com.terraformersmc:modmenu:9.0.0-pre.1"
    compileOnly project(":shared")
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileJava {
    source project(':shared').sourceSets.main.allSource
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
