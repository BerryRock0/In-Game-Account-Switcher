buildscript {
    repositories {
        maven { url "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "InGameAccountSwitcher-Forge-1.8"
version = project.mod_version
evaluationDependsOn(':shared')

dependencies {
    compile project(":shared")
    implementation 'org.jetbrains:annotations:23.0.0'
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", project.version
    filesMatching("mcmod.info") {
        expand "version": project.version
    }
}

compileJava {
    source project(':shared').sourceSets.main.allSource
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
    manifest {
        attributes([
                "FMLAT": "ias_at.cfg"
        ])
    }
}