plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "InGameAccountSwitcher-Fabric-1.17"
version = project.mod_version
evaluationDependsOn(':shared')

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://api.modrinth.com/maven/" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.17.1"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.46.1+1.17"
    modImplementation "com.terraformersmc:modmenu:2.0.17"
    modRuntimeOnly "maven.modrinth:lazydfu:0.1.2"
    compileOnly project(":shared")
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

processResources {
    from project(":shared").sourceSets.main.resources
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

compileJava {
    source project(':shared').sourceSets.main.allSource
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}